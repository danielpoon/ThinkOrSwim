# InSync
# Drew Griffith

#hint: Mean Reversion ENTRY Strategy. The default inputs are based on stocks that are more volatile in nature. If you prefer to trade less volatile stocks, you should lower the extremities input. There are additional filters in place to ensure a better entry signal. The strategy is based on closing prices of the day of signal, so buy as close to the EOD as possible. The target is the high or open price of the day of entry. Ideal hold times are less than 5 days. Normally, around day 5, the position moves to breakeven. Optimized for use on daily charts.

declare lower;
input audibleAlerts = NO;
input paintbars = YES;
input extremities = 2.0;
input oversold = 50;
input overbought = 450;
input bbp_length = 20;
input mfi_length = 14;
input sto_length = 14;
def rsi2_length = 2;
def rsi14_length = 14;

# study definitions
def rsi2 = RSI(length = rsi2_length);
def rsi14 = RSI(length = rsi14_length);
def mfi = MoneyFlowIndex(length = mfi_length);
def sto = StochasticFull("k period" = sto_length);
def bp = BollingerPercentB(length = bbp_length, "average type" = "EXPONENTIAL");
def bbp = if bp >= 100 then 100 else if bp <= 0 then 0 else bp;

# plots
plot insync = Round(bbp + rsi2 + rsi14 + mfi + sto, numberofdigits = -1);
plot pos = overbought;
plot neg = oversold;
insync.AssignValueColor(if insync >= overbought then Color.RED else if insync <= oversold then Color.GREEN else Color.GRAY);
insync.SetLineWeight(2);
pos.AssignValueColor(Color.GREEN);
neg.AssignValueColor(Color.RED);
pos.HideTitle();
neg.HideTitle();

# Large move down / up
def ext = if close < open and ((high / close - 1) * 100) >= extremities then 1 else if close > open and ((close / low - 1) * 100) >= extremities then 1 else Double.NaN;

plot signal = if ext and insync <= neg then insync else if ext and insync >= pos then insync else Double.NaN;
#signal.hide();
signal.SetPaintingStrategy(paintingstrategy = PaintingStrategy.POINTS);
signal.SetLineWeight(5);
signal.AssignValueColor(if insync <= neg then color.GREEN else if insync >= pos then color.RED else Color.YELLOW);

AssignPriceColor(if paintbars and signal then Color.YELLOW else Color.CURRENT);

# bullish / bearish divergence
input ntrend = 30;
def ll = lowest(low, ntrend);
def cl = lowest(close, ntrend);
def strat_ll = round(lowest(insync, ntrend));  #round to nearest whole number
plot DLow = if low == ll and close == cl and (insync - strat_ll) > 10 and strat_ll <= oversold and ext then insync else Double.NaN;
DLow.SetPaintingStrategy(PaintingStrategy.POINTS);
DLow.SetLineWeight(1);
DLow.SetDefaultColor(Color.YELLOW);

def hh = highest(high, ntrend);
def ch = highest(close, ntrend);
def strat_hh = round(highest(insync, ntrend));  #round to nearest whole number
plot DHigh = if high == hh and close == ch and (strat_hh - insync) > 10 and strat_hh >= overbought and ext then insync else Double.NaN;
DHigh.SetPaintingStrategy(PaintingStrategy.POINTS);
DHigh.SetLineWeight(1);
DHigh.SetDefaultColor(Color.YELLOW);

Alert(audibleAlerts and signal, GetSymbol() + " at exhaustion.", Alert.BAR, Sound.Ding);

## Needed for Watchlist box painting
#AssignBackgroundColor(if LE then Color.GREEN else Color.GRAY);
