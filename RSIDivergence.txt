# RSI Divergence
# Drew Griffith

#hint: signal if the rsi is in a divergence using RSI Histogram.

declare lower;

input rsi_length = 14;
input ntrend = 60;
input ma_length = 60;
input rsi_OB = 20;
input rsi_OS = -20;

# global definitions
def hi = high;
def lo = low;
def cl = close;

def rsi = Round(RSI(length = rsi_length, price = cl), 1);
def rsi_ma = Round(ExpAverage(rsi, length = ma_length), 1);
plot diff = Round(rsi - rsi_ma, 0);
diff.SetPaintingStrategy(PaintingStrategy.HISTOGRAM);
diff.SetLineWeight(3);
diff.AssignValueColor(if diff >= rsi_OB then Color.RED else if diff <= rsi_OS then Color.GREEN else Color.GRAY);

# bearish divergence definitions
plot strat_hh = Round(Highest(diff, ntrend));  #round to nearest whole number
strat_hh.SetDefaultColor(Color.DARK_GRAY);
strat_hh.SetLineWeight(1);

# bullish divergence definitions
plot strat_ll = Round(Lowest(diff, ntrend));  #round to nearest whole number
strat_ll.SetDefaultColor(Color.DARK_GRAY);
strat_ll.SetLineWeight(1);

plot diff_high = rsi_OB;
diff_high.SetDefaultColor(Color.DARK_GRAY);
diff_high.SetLineWeight(1);
diff_high.Hide();
diff_high.HideTitle();

plot diff_low = rsi_OS;
diff_low.SetDefaultColor(Color.DARK_GRAY);
diff_low.SetLineWeight(1);
diff_low.Hide();
diff_low.HideTitle();

plot rsitrend = Round(InertiaAll(diff, ntrend), 2);
rsitrend.SetLineWeight(2);
rsitrend.SetDefaultColor(Color.RED);

def div = round(rsitrend-rsitrend[ntrend-1],1);
AddLabel(yes, "RSI Div: " + div, if div >= 5 then Color.GREEN else if div <= -5 then Color.RED else Color.GRAY);
