# RSI Divergence
# Drew Griffith

#hint: signal if the rsi is in a divergence.

declare lower;

input nrsi = 14;
input overbought = 70;
input oversold = 30;
input ntrend = 60;
input trendline = {EMA, SMA, default LRL, WMA};

# global definitions
def h = high;
def l = low;
def c = close;

# rsi wilder with divergence markers
def netchgavg = WildersAverage(c - c[1], nrsi);
def totchgavg = WildersAverage(AbsValue(c - c[1]), nrsi);
def chgratio = if totchgavg != 0 then netchgavg / totchgavg else 0;
plot rsi = Round(50 * (chgratio + 1), 0);
rsi.SetLineWeight(2);
rsi.AssignValueColor(if rsi < oversold then Color.GREEN else if  rsi > overbought then Color.RED else Color.GRAY);

plot rsitrend;
switch (trendline) {
case EMA:
    rsitrend = Round(ExpAverage(rsi, ntrend), 2);
case SMA:
    rsitrend = Round(Average(rsi, ntrend), 2);
case LRL:
    rsitrend = Round(InertiaAll(rsi, ntrend), 2);
case WMA:
    rsitrend = Round(WMA(rsi, ntrend), 2);
}
rsitrend.SetLineWeight(2);
rsitrend.SetDefaultColor(Color.RED);

plot rsiob = overbought;
rsiob.SetLineWeight(1);
rsiob.SetDefaultColor(Color.DARK_GRAY);
plot rsios = oversold;
rsios.SetLineWeight(1);
rsios.SetDefaultColor(Color.DARK_GRAY);

# bearish divergence
def hh = Highest(h, ntrend);
def ch = Highest(c, ntrend);
def strat_hh = round(Highest(rsi, ntrend));  #round to nearest whole number
plot DHigh = if h == hh and c == ch and rsi < strat_hh and rsi > overbought then rsi else Double.NaN;
DHigh.SetPaintingStrategy(PaintingStrategy.POINTS);
DHigh.SetLineWeight(3);
DHigh.SetDefaultColor(Color.YELLOW);

# bullish divergence
def ll = lowest(l, ntrend);
def cl = lowest(c, ntrend);
def strat_ll = round(lowest(rsi, ntrend));  #round to nearest whole number
plot DLow = if l == ll and c == cl and rsi > strat_ll and rsi < oversold then rsi else Double.NaN;
DLow.SetPaintingStrategy(PaintingStrategy.POINTS);
DLow.SetLineWeight(3);
DLow.SetDefaultColor(Color.YELLOW);'

#alert(DHigh, "divergent high", alert.bar, sound.bell);
#alert(DLow, "divergent low", alert.bar, sound.bell);

#plot BullDivScan = DLow;
#plot BearDivScan = DHigh;
