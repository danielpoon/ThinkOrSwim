# VolumeBuySell
# drew griffith

#HINT: This study color codes volume by amount of volume on up-tick versus amount of volume on down-tick. Adds a label showing volume as the percent of the average volume.

declare lower;
input length = 50;

def O = open;
def H = high;
def C = close;
def L = low;
def V = volume;
def Buying = V * (C - L) / (H - L);
def Selling = V * (H - C) / (H - L);

# Selling Volume
plot SV = Selling;
SV.SetPaintingStrategy(PaintingStrategy.HISTOGRAM);
SV.SetDefaultColor(Color.RED);
SV.HideTitle();
SV.HideBubble();
SV.SetLineWeight(5);

# Buying Volume
# Note that Selling + Buying Volume = Volume.
plot BV = volume;
BV.SetPaintingStrategy(PaintingStrategy.HISTOGRAM);
BV.AssignValueColor(if Buying > Selling then Color.GREEN else if Buying < Selling then Color.RED else color.Gray);
# BV.HideTitle();
# BV.HideBubble();
BV.SetLineWeight(5);

##### calc label ######
plot VolAvg = Average(BV, length);
VolAvg.SetDefaultColor(GetColor(8));

# automatically take aggPeriod of the current chart
def AggPeriod = GetAggregationPeriod();
def Pct_Avg = volume(period = AggPeriod) / Average(volume(period = AggPeriod), length) * 100;
AddLabel(yes, "Vol = " + Round(Pct_Avg, 1) + " % of the " + length + "-period-avg" , Color.PINK);
