# RSI HISTOGRAM
# Drew Griffith

#hint: signal if the rsi is in a divergence using RSI Histogram.

declare lower;
input rsi_length = 14;
input ntrend = 10;
input extreme = 26;

# global definitions
def hi = high;
def lo = low;
def cl = close;
def op = open;

def rsi = Round(RSI(length = rsi_length, price = cl), 1);
plot diff = Round(rsi - 50, 1); #50 is the midpoint
diff.SetPaintingStrategy(PaintingStrategy.HISTOGRAM);
diff.SetLineWeight(3);
diff.DefineColor("Overbought and Up", Color.CYAN);
diff.DefineColor("Overbought and Down", Color.DARK_GREEN);
diff.DefineColor("Positive and Up", Color.GREEN);
diff.DefineColor("Positive and Down", Color.DARK_GREEN);
diff.DefineColor("Oversold and Down", Color.MAGENTA);
diff.DefineColor("Oversold and Up", Color.DARK_RED);
diff.DefineColor("Negative and Down", Color.RED);
diff.DefineColor("Negative and Up", Color.DARK_RED);

diff.AssignValueColor(if diff >= 20 and diff >= diff[1] then diff.Color("Overbought and Up") else if diff >= 20 and diff < diff[1] then diff.Color("Overbought and Down") else if diff >= 0 then if diff >= diff[1] then diff.Color("Positive and Up") else diff.Color("Positive and Down") else if diff <= -20 and diff <= diff[1] then diff.Color("Oversold and Down") else if diff <= -20 and diff > diff[1] then diff.Color("Oversold and Up") else if diff < diff[1] then diff.Color("Negative and Down") else diff.Color("Negative and Up"));

# lower boundries of rsi over the last ntrend periods
plot strat_ll = Round(Lowest(diff, ntrend));  #round to nearest whole number
strat_ll.AssignValueColor(Color.DARK_GRAY);
strat_ll.SetLineWeight(1);

# upper boundries of rsi over the last ntrend periods
plot strat_hh = Round(Highest(diff, ntrend));  #round to nearest whole number
strat_hh.AssignValueColor(Color.DARK_GRAY);
strat_hh.SetLineWeight(1);

# plotting signals for bullish / bearish divergences
def ll = Lowest(lo, ntrend);
def hh = Highest(hi, ntrend);

# looking for any bearish divergence
plot beardiv = if cl >= hh[1] and diff < strat_hh and strat_hh >= extreme then diff else Double.NaN;
beardiv.SetPaintingStrategy(PaintingStrategy.ARROW_DOWN);
beardiv.SetLineWeight(1);
beardiv.SetDefaultColor(color = Color.WHITE);

# looking for any bullish divergence
plot bulldiv = if cl <= ll[1] and diff > strat_ll and strat_ll <= -extreme then diff else Double.NaN;
bulldiv.SetPaintingStrategy(PaintingStrategy.ARROW_UP);
bulldiv.SetLineWeight(1);
bulldiv.SetDefaultColor(color = Color.WHITE);

input showlabel = yes;
AddLabel(showlabel, "RSI = " + rsi, if diff <= -20 then Color.GREEN else if diff >= 20 then Color.RED else Color.GRAY);
